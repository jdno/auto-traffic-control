
/* The authors of auto-traffic-control release this file under either Apache v2.0 or MIT license terms. */

import com.github.jk1.license.render.*

plugins {
	id 'com.github.jk1.dependency-license-report' version '2.0'
	id 'java'
	id 'java-library'
	id 'com.google.protobuf' version '0.8.18'
	id 'maven-publish'
}

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	if ( JavaVersion.current().isJava9Compatible() ) {
		compileOnly 'org.apache.tomcat:annotations-api:6.0.53'
	}
	else {
		compileOnly 'javax.annotation:javax.annotation-api:1.3.1'
	}

	implementation 'io.grpc:grpc-protobuf:1.48.1'
	implementation 'io.grpc:grpc-stub:1.48.1'
}

group = 'dev.jdno.atc'
version = '0.3.2'
description = 'Wraps the generated grpc classes of jdno Auto-Traffic-Control game.'
sourceCompatibility = '1.8'

def tasksAfterProto = [] // ¶ adapted from https://github.com/google/protobuf-gradle-plugin/issues/136#issuecomment-316185539
tasks.all { task ->
	if ( task.name.startsWith( 'sourcesJar' ) ) {
		tasksAfterProto.push( task )
	}
}

licenseReport {
	// ¶ outputDir = '${project.buildDir}/reports/dependency-license'
	renderers = [ new JsonReportRenderer( 'dependency-licenses.json', false ) ]
}

protobuf {
	protoc {
		artifact = 'com.google.protobuf:protoc:3.21.1'
	}
	plugins {
        	grpc {
			artifact = 'io.grpc:protoc-gen-grpc-java:1.48.1'
		}
		
	}
	generateProtoTasks {
		ofSourceSet('main')*.plugins {
			grpc {}
		}
		tasksAfterProto.each {
			it.dependsOn( 'generateProto' )
		}
	}
	generatedFilesBaseDir = 'src/generated/'
}

sourceSets {
    main {
	proto {
		srcDir '../../api'
	}
        java {
            srcDirs 'src/generated/main/grpc'
            srcDirs 'src/generated/main/java'
        }
    }
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of( 8 )
	}
	withSourcesJar()
}

compileJava {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
}

javadoc {
	failOnError = false
	options.setEncoding( 'utf8' )
}

jar {
	manifest {
		attributes(
			'Automatic-Module-Name' : 'dev.jdno.atc',
			'LICENSE' : 'Released under Apache v2.0 or MIT license terms'
		)
	}
}

publishing {
	publications {
		maven( MavenPublication ) {
			from( components.java )
			pom {
				name = project.name
				inceptionYear = '2022'
				url = 'https://auto-traffic-control.com/'
				packaging = 'jar'
				licenses { [
					license {
						name = 'Apache v2.0'
						url = 'https://www.apache.org/licenses/LICENSE-2.0'
					},
					license {
						name = 'MIT v1.0'
						url = 'https://www.mit.edu/~amini/LICENSE.md'
					}
				] }
				developers {
					developer {
						id = 'jdno'
						name = 'Jan David'
						email = ''
					}
				}
				scm {
					connection = 'https://github.com/jdno/auto-traffic-control'
					developerConnection = 'https://github.com/jdno/auto-traffic-control.git'
					url = 'https://github.com/jdno/auto-traffic-control.git'
				}
			}
		}
	}
}


































